# vim: ft=PKGBUILD
# Maintainer: Jack L. Frost <fbt@fleshless.org>
# Contributor: Eric Vidal <eric@obarun.org>
# % Trigger: 1441370137 %

pkgname=('vdev-git' 'vdevfs-git' 'vdev-libudev-compat-git')
pkgver=r679.a81f7bc
pkgrel=5
pkgdesc='A virtual device manager for *nix'
url='https://github.com/jcnelson/vdev.git'
arch=( 'x86_64' 'i686' )
license=( 'custom:ISC' )
makedepends=( 'libpstat-git' 'fskit-git' 'squashfs-tools' 'dash' )

source=( "${pkgname}::git+${url}" 'vdev_hook' 'vdev_install' 'libudev.so.1.6.3' 'vdevd.boot.conf')

pkgver() {
	cd "${pkgname}"

	if git_version=$( git describe --long --tags 2>/dev/null ); then
		IFS='-' read last_tag tag_rev commit <<< "$git_version"
		printf '%s.r%s.%s' "$last_tag" "$tag_rev" "$commit"
	else
		printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	fi
}

build() {
	cd "${pkgname}"
	
	make PREFIX=/usr -C vdevd
	make PREFIX=/usr -C hwdb
	make PREFIX=/usr -C fs

	make PREFIX=/usr -C libudev-compat
}

package_vdev-git() {
	depends=( 'util-linux' 'kmod' 'iproute2' 'sed' 'grep' 'dash' 'makedev' )
	optdepends=( 'lvm2' 'device-mapper' )
	provides=( 'vdev' 'udev' )
	conflicts=( 'vdev' 'udev' )
	replaces=('udev')

	cd "$pkgname"

	# vdevd
	make -C vdevd \
		DESTDIR="$pkgdir" \
		PREFIX='/usr' \
		ETCDIR='/etc' \
		BINDIR='/usr/bin' \
		SBINDIR='/usr/bin' \
	install

	# config
	make -C example \
		DESTDIR="$pkgdir" \
		PREFIX='/usr' \
		ETCDIR='/etc' \
	install

	# hwdb
	make DESTDIR="${pkgdir}" PREFIX=/usr -C hwdb install

	cd "$pkgdir"

	# Config files
	backup+=( etc/vdev/actions/*.act )
	backup+=( etc/vdev/*.conf )

	# Fix the log path
	sed -i "s%logfile=/usr/run%logfile=/var/log%" etc/vdev/vdevd.conf

	# Fix the run path
	sed -i "s%pidfile=/usr/run%pidfile=/run%" etc/vdev/vdevd.conf
	
	#cd "$srcdir/$pkgbase/example"
	#install -dm755 "$pkgdir/etc/vdev/acls"
	#cp acls/* "$pkgdir/etc/vdev/acls"
	
	install -D -m644 "${srcdir}/vdev_hook" "${pkgdir}/usr/lib/initcpio/hooks/vdev"
	install -D -m644 "${srcdir}/vdev_install" "${pkgdir}/usr/lib/initcpio/install/vdev"
	install -D -m644 "${srcdir}/vdevd.boot.conf" "${pkgdir}/etc/vdev/vdevd.boot.conf"
	
	# Install the licence
	install -Dm755 "$srcdir/$pkgbase/LICENSE.ISC" "$pkgdir/usr/share/licenses/$pkgname"
}

package_vdevfs-git() {
	depends=( 'libpstat' 'fskit' 'fuse' 'libstdc++5' )
	provides=( 'vdevfs' )
	conflicts=( 'vdevfs' )

	cd vdev-git
	make -C fs \
		DESTDIR="$pkgdir" \
		PREFIX='/usr' \
		SBINDIR='/usr/bin' \
	install

	# Install the licence
	install -Dm755 "$srcdir/$pkgbase/LICENSE.ISC" "$pkgdir/usr/share/licenses/$pkgname"
}

package_vdev-libudev-compat-git() {
	provides=( 'libudev' 'libudev.so' 'libudev.so.1')
	conflicts=( 'libudev' 'libudev.so')
	replaces=('libudev' 'libudev.so')

	cd vdev-git
	make -C libudev-compat \
		DESTDIR="$pkgdir" \
		PREFIX=/usr \
	install
	
	cd "$srcdir/"
	cp libudev.so.1.6.3 "$pkgdir/usr/lib/"
	cd "$pkgdir/usr/lib"
	rm libudev.so.1.5.2
	ln -sf libudev.so.1 libudev.so
	ln -sf libudev.so.1.5.2 libudev.so.1
	ln -sf libudev.so.1.6.3 libudev.so.1.5.2
	
	# Install the licence
	install -Dm755 "$srcdir/$pkgbase/LICENSE.ISC" "$pkgdir/usr/share/licenses/$pkgname"
}

sha1sums=('SKIP'
          '31ab831ba340ca8bc3597098af83700589929811'
          'ba7e44737771576c1144689b1f3a037d77434006'
          'd5ebd1accc05a2e98f6637ecce1a79acc83bd49c'
          'f9ef48b5f0deaf3a18145ca8f82c9b39be71777e')
