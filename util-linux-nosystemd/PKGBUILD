# Maintainer: Eric Vidal <eric@obarun.org>
# based on the	original https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/util-linux&id=6b9f34ed258a54c4d4a27f2edabaaf5bc5e616df
# 						Maintainer: Tom Gundersen <teg@jklm.no>
# 						Maintainer: Dave Reisner <dreisner@archlinux.org>
# 						Contributor: judd <jvinet@zeroflux.org>

pkgbase=util-linux-nosystemd
pkgname=(util-linux-nosystemd libutil-linux-nosystemd)
pkgver=2.27
pkgrel=4
pkgdesc="Miscellaneous system utilities for Linux"
url="https://www.kernel.org/pub/linux/utils/util-linux/"
arch=('x86_64')
makedepends=('python')
license=('GPL2')
options=('strip' 'debug')
validpgpkeys=('B0C64D14301CC6EFAEDF60E4E4B71D5EEC39C284')  # Karel Zak
source=("https://www.kernel.org/pub/linux/utils/util-linux/v2.27/${pkgname%-nosystemd}-$pkgver.tar.xz"
        uuidd.tmpfiles
        pam-{login,common,su})
md5sums=('5b06bbda9309624ee7add15bc8d8ca22'
         'a39554bfd65cccfd8254bb46922f4a67'
         '4368b3f98abd8a32662e094c54e7f9b1'
         'a31374fef2cba0ca34dfc7078e2969e4'
         'fa85e5cce5d723275b14365ba71a8aad')

build() {
  cd "${pkgname%-nosystemd}-$pkgver"

  ./configure --prefix=/usr \
              --libdir=/usr/lib \
              --bindir=/usr/bin \
              --localstatedir=/run \
              --enable-fs-paths-extra=/usr/bin \
              --enable-raw \
              --enable-vipw \
              --enable-newgrp \
              --enable-chfn-chsh \
              --enable-write \
              --enable-mesg \
              --enable-usrdir-path \
              --with-python=3 \
              --with-systemdsystemunitdir=no \
			  --with-systemd=no
			  #--enable-libmount-force-mountinfo \ #recommended for systemd
              #--enable-socket-activation \ #??? option not available 
			  	
  make
}

package_util-linux-nosystemd() {
  conflicts=('util-linux' 'util-linux-ng' 'eject' 'zramctl')
  provides=("util-linux=$pkgver" "util-linux-ng=$pkgver" 'eject' 'zramctl')
  replaces=('zramctl' 'util-linux')
  depends=('pam' 'shadow' 'coreutils' 'libutil-linux-nosystemd' 'eudev-libgudev' 'libcap-ng')
  optdepends=('python: python bindings to libmount')
  install=util-linux.install
  groups=('base' 'base-devel')
  backup=(etc/pam.d/chfn
          etc/pam.d/chsh
          etc/pam.d/login
          etc/pam.d/su
          etc/pam.d/su-l)

  cd "${pkgbase%-nosystemd}-$pkgver"

  make DESTDIR="$pkgdir" install

  # setuid chfn and chsh
  chmod 4755 "$pkgdir"/usr/bin/{newgrp,ch{sh,fn}}

  # install PAM files for login-utils
  install -Dm644 "$srcdir/pam-common" "$pkgdir/etc/pam.d/chfn"
  install -m644 "$srcdir/pam-common" "$pkgdir/etc/pam.d/chsh"
  install -m644 "$srcdir/pam-login" "$pkgdir/etc/pam.d/login"
  install -m644 "$srcdir/pam-su" "$pkgdir/etc/pam.d/su"
  install -m644 "$srcdir/pam-su" "$pkgdir/etc/pam.d/su-l"

  # include tmpfiles fragment for uuidd
  # TODO(dreisner): offer this upstream?
  install -Dm644 "$srcdir/uuidd.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/uuidd.conf"

  # usrmove
  # adjust for usrmove
  # TODO(dreisner): fix configure.ac upstream so that this isn't needed
  cd "$pkgdir"
  mv {,usr/}sbin/* usr/bin
  rmdir sbin usr/sbin

  ### create libutil-linux split
  ### runtime libs are shipped as part of libutil-linux
  rm "$pkgdir"/usr/lib/lib*.{a,so}*
}

package_libutil-linux-nosystemd() {
  pkgdesc="util-linux runtime libraries"
  depends=('glibc')
  provides=("libutil-linux=$pkgver" 'libblkid.so' 'libfdisk.so' 'libmount.so' 'libsmartcols.so' 'libuuid.so')
  replaces=('libutil-linux')
  conflicts=('libutil-linux')
  	
  make -C "${pkgbase%-nosystemd}-$pkgver" DESTDIR="$pkgdir" install-usrlib_execLTLIBRARIES
}
