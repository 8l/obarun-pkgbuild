# Maintainer: Eric Vidal <eric@obarun.org>
# based on the	original https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/pulseaudio
# 						Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# 						Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# 						Contributor: Corrado Primier <bardo@aur.archlinux.org>
# 						Contributor: William Rea <sillywilly@gmail.com>

pkgbase=pulseaudio-nosystemd
pkgname=(pulseaudio-nosystemd libpulse-nosystemd pulseaudio-{gconf-nosystemd,zeroconf-nosystemd,lirc-nosystemd,xen-nosystemd,jack-nosystemd,bluetooth-nosystemd,equalizer-nosystemd})
pkgdesc="A featureful, general-purpose sound server"
pkgver=6.99.2
pkgrel=1
arch=(i686 x86_64)
url="http://www.freedesktop.org/wiki/Software/PulseAudio"
license=(LGPL)
makedepends=(libasyncns libcap attr libxtst libsm libsndfile libtool rtkit libsoxr
             speexdsp tdb dbus avahi bluez bluez-libs gconf intltool jack2-dbus sbc
             lirc openssl xenstore fftw orc json-c gtk3 webrtc-audio-processing
             check)
options=(!emptydirs)
source=(http://freedesktop.org/software/${pkgbase%-nosystemd}/releases/${pkgbase%-nosystemd}-$pkgver.tar.xz
        padsp-lib32.patch)
sha256sums=('e7fbc3d50647ab3c022a1761c26d6337c9820ff031cbedcdd6e7b602d2b63359'
            '7832fc59df76538ff10aedd297c03cb7ff117235da8bfad26082994bb5b84332')

prepare() {
  cd ${pkgbase%-nosystemd}-$pkgver
}

build() {
  cd ${pkgbase%-nosystemd}-$pkgver

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --with-udev-rules-dir=/usr/lib/udev/rules.d \
    --with-database=tdb \
    --disable-tcpwrap \
    --disable-bluez4 \
    --disable-samplerate \
    --disable-rpath \
    --disable-default-build-tests \
	--disable-systemd-daemon \
	--disable-systemd-login \
	--disable-systemd-journal \
	--with-zsh-completion=/usr/share/zsh/site-functions
  # fight unused direct deps
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package_pulseaudio-nosystemd() {
  depends=("libpulse=$pkgver-$pkgrel" rtkit libltdl speexdsp tdb orc
           webrtc-audio-processing sbc libsoxr)
  optdepends=('pulseaudio-alsa: ALSA configuration (recommended)')
  provides=('pulseaudio')
  replaces=('pulseaudio')
  conflicts=('pulseaudio')
  backup=(etc/pulse/{daemon.conf,default.pa,system.pa})
  install=pulseaudio.install

  cd ${pkgbase%-nosystemd}-$pkgver

  _pulsever=$(./git-version-gen .tarball-version)
  [[ $_pulsever = *.*.* ]] && _pulsever=${_pulsever%.*}

  make -j1 DESTDIR="$pkgdir" install \
    bashcompletiondir=/usr/share/bash-completion/completions

  cd "$pkgdir"
  patch -Np1 -i "$srcdir/padsp-lib32.patch"

  sed -e '/flat-volumes/iflat-volumes=no' \
      -i etc/pulse/daemon.conf

  # Superseded by socket activation
  sed -e '/autospawn/iautospawn=no' \
      -i etc/pulse/client.conf

  # Disable cork-request module, can result in e.g. media players unpausing
  # when there's a Skype call incoming
  sed -e 's|/usr/bin/pactl load-module module-x11-cork-request|#&|' \
      -i usr/bin/start-pulseaudio-x11

  #rm etc/dbus-1/system.d/pulseaudio-system.conf
  mkdir -p "$pkgdir"/usr/share/dbus-1/system.d
  mv etc/dbus-1/system.d/pulseaudio-system.conf \
     "$pkgdir/usr/share/dbus-1/system.d"
     
  #mkdir usr/lib/systemd/user/sockets.target.wants
  #ln -s ../pulseaudio.socket \
  #  usr/lib/systemd/user/sockets.target.wants/pulseaudio.socket

### Split libpulse

  mkdir -p "$srcdir"/libpulse/{etc/pulse,usr/{bin,lib/pulseaudio,share/man/man{1,5}}}

  mv etc/pulse/client.conf \
     "$srcdir/libpulse/etc/pulse"

  mv usr/bin/pa{cat,ctl,dsp,mon,play,rec,record} \
     "$srcdir/libpulse/usr/bin"

  mv usr/lib/libpulse{,-simple,-mainloop-glib}.so* \
     usr/lib/{cmake,pkgconfig} \
     "$srcdir/libpulse/usr/lib"

  mv usr/lib/pulseaudio/libpulse{dsp,common-*}.so \
     "$srcdir/libpulse/usr/lib/pulseaudio"

  mv usr/include \
     "$srcdir/libpulse/usr"

  mv usr/share/man/man1/pa{cat,ctl,dsp,mon,play,rec,record}.1 \
     "$srcdir/libpulse/usr/share/man/man1"

  mv usr/share/man/man5/pulse-client.conf.5 \
     "$srcdir/libpulse/usr/share/man/man5"

  mv usr/share/vala \
     "$srcdir/libpulse/usr/share"

### Split modules

  mkdir -p "$srcdir"/{gconf,zeroconf,lirc,xen,jack,bluetooth,equalizer}/usr/lib/pulse-$_pulsever/modules \
           "$srcdir"/{gconf/usr/lib/pulse,equalizer/usr/bin}

  mv usr/lib/pulse-$_pulsever/modules/module-gconf.so \
     "$srcdir/gconf/usr/lib/pulse-$_pulsever/modules"
  mv usr/lib/pulse/gconf-helper \
     "$srcdir/gconf/usr/lib/pulse"

  mv usr/lib/pulse-$_pulsever/modules/{libavahi-wrap,module-{zeroconf-{publish,discover},raop-discover}}.so \
     "$srcdir/zeroconf/usr/lib/pulse-$_pulsever/modules"

  mv usr/lib/pulse-$_pulsever/modules/module-lirc.so \
     "$srcdir/lirc/usr/lib/pulse-$_pulsever/modules"

  mv usr/lib/pulse-$_pulsever/modules/module-xenpv-sink.so \
     "$srcdir/xen/usr/lib/pulse-$_pulsever/modules"

  mv usr/lib/pulse-$_pulsever/modules/module-jack{-sink,-source,dbus-detect}.so \
     "$srcdir/jack/usr/lib/pulse-$_pulsever/modules"

  mv usr/lib/pulse-$_pulsever/modules/{libbluez5-util,module-{bluetooth-{discover,policy},bluez5-{discover,device}}}.so \
     "$srcdir/bluetooth/usr/lib/pulse-$_pulsever/modules"

  mv usr/lib/pulse-$_pulsever/modules/module-equalizer-sink.so \
     "$srcdir/equalizer/usr/lib/pulse-$_pulsever/modules"
  mv usr/bin/qpaeq \
     "$srcdir/equalizer/usr/bin"
}

package_libpulse-nosystemd() {
  pkgdesc="$pkgdesc (client library)"
  depends=(dbus libasyncns libcap libxtst libsm libsndfile json-c)
  provides=('libpulse')
  replaces=('libpulse')
  conflicts=('libpulse')
  backup=(etc/pulse/client.conf)

  mv "$srcdir"/libpulse/* "$pkgdir"
}

package_pulseaudio-gconf-nosystemd(){
  pkgdesc="GConf support for PulseAudio"
  depends=("pulseaudio=$pkgver-$pkgrel" gconf)
  provides=('pulseaudio-gconf')
  replaces=('pulseaudio-gconf')
  conflicts=('pulseaudio-gconf')

  mv "$srcdir"/gconf/* "$pkgdir"
}

package_pulseaudio-zeroconf-nosystemd(){
  pkgdesc="Zeroconf support for PulseAudio"
  depends=("pulseaudio=$pkgver-$pkgrel" avahi openssl)
  provides=('pulseaudio-zeroconf')
  replaces=('pulseaudio-zeroconf')
  conflicts=('pulseaudio-zeroconf')
  
  mv "$srcdir"/zeroconf/* "$pkgdir"
}

package_pulseaudio-lirc-nosystemd(){
  pkgdesc="IR (lirc) support for PulseAudio"
  depends=("pulseaudio=$pkgver-$pkgrel" lirc)
  provides=('pulseaudio-lirc')
  replaces=('pulseaudio-lirc')
  conflicts=('pulseaudio-lirc')
  
  mv "$srcdir"/lirc/* "$pkgdir"
}

package_pulseaudio-xen-nosystemd(){
  pkgdesc="Xen support for PulseAudio"
  depends=("pulseaudio=$pkgver-$pkgrel" xenstore)
  provides=('pulseaudio-xen')
  replaces=('pulseaudio-xen')
  conflicts=('pulseaudio-xen')
  license=(GPL2)

  mv "$srcdir"/xen/* "$pkgdir"
}

package_pulseaudio-jack-nosystemd(){
  pkgdesc="Jack support for PulseAudio"
  depends=("pulseaudio=$pkgver-$pkgrel" jack2)
  provides=('pulseaudio-jack')
  replaces=('pulseaudio-jack')
  conflicts=('pulseaudio-jack')
  
  mv "$srcdir"/jack/* "$pkgdir"
}

package_pulseaudio-bluetooth-nosystemd(){
  pkgdesc="Bluetooth support for PulseAudio"
  depends=("pulseaudio=$pkgver-$pkgrel" bluez bluez-libs)
  provides=('pulseaudio-bluetooth')
  replaces=('pulseaudio-bluetooth')
  conflicts=('pulseaudio-bluetooth')
  
  mv "$srcdir"/bluetooth/* "$pkgdir"
}

package_pulseaudio-equalizer-nosystemd(){
  pkgdesc="Equalizer for PulseAudio"
  depends=("pulseaudio=$pkgver-$pkgrel" python-pyqt4 fftw)
  provides=('pulseaudio-equalizer')
  replaces=('pulseaudio-equalizer')
  conflicts=('pulseaudio-equalizer')
  license=(AGPL3)

  mv "$srcdir"/equalizer/* "$pkgdir"
}
