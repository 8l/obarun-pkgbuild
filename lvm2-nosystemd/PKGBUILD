# Maintainer: Eric Vidal <eric@obarun.org>
# based on the	original https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/lvm2
# 						Maintainer: Eric Bélanger <eric@archlinux.org>
# 						Maintainer: Thomas Bächler <thomas@archlinux.org>

pkgbase=lvm2
pkgname=('lvm2-nosystemd' 'device-mapper-nosystemd')
pkgver=2.02.132
pkgrel=1
arch=('x86_64')
url="http://sourceware.org/lvm2/"
license=('GPL2' 'LGPL2.1')
makedepends=('eudev-libgudev' 'thin-provisioning-tools')
groups=('base')
source=(ftp://sources.redhat.com/pub/lvm2/LVM2.${pkgver}.tgz
        lvm2_install
        lvm2_hook
        11-dm-initramfs.rules)
sha1sums=('45a486e2aba07fa50fd663d0c550ba9a136aead2'
          '40ef991650555b904e73bcc3f344d736722e27ca'
          'ff0fdf0a3005a41acd4b36865056109effc3474b'
          'f6a554eea9557c3c236df2943bb6e7e723945c41')

prepare() {
  cd LVM2.${pkgver}

  # enable lvmetad
  sed -i 's|use_lvmetad = 0|use_lvmetad = 1|' conf/example.conf.in
}

build() {
	
	cp -a LVM2.${pkgver} LVM2-initramfs
	cd LVM2-initramfs
  ./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--sbindir=/usr/bin \
        --enable-pkgconfig \
        --enable-readline \
        --enable-dmeventd \
        --enable-cmdlib \
        --enable-applib \
        --enable-udev_sync \
        --enable-udev_rules \
        --enable-udev-rule-exec-detection \
        --enable-lvmetad \
        --with-thin=internal \
        --with-cache=internal \
		--disable-udev-systemd-background-jobs \
		--with-udev-prefix=/usr \
		--with-udevdir=/usr/lib/udev/rules.d \
        --with-systemdsystemunitdir=no \
        --with-tmpfilesdir=no \
		--with-default-locking-dir=/run/lock/lvm \
		--with-default-pid-dir=/run \
		--with-default-dm-run-dir=/run \
		--with-default-run-dir=/run/lvm \
		--with-lvmetad-pidfile=/run \
		--with-dmeventd-path=/usr/bin \
		
  cd udev
  make 69-dm-lvm-metad.rules
}

package_device-mapper-nosystemd() {
  pkgdesc="Device mapper userspace library and tools"
  url="http://sourceware.org/dm/"
  depends=('glibc')
  provides=('device-mapper')
  conflicts=('device-mapper')
  replaces=('device-mapper')
  cd LVM2-initramfs
  make DESTDIR="${pkgdir}" install_device-mapper
  # extra udev rule for device-mapper in initramfs
  install -D -m644 "${srcdir}/11-dm-initramfs.rules" "${pkgdir}/usr/lib/initcpio/udev/11-dm-initramfs.rules"
 }

package_lvm2-nosystemd() {
  pkgdesc="Logical Volume Manager 2 utilities"
  depends=('bash' "device-mapper-nosystemd>=${pkgver}" 'readline' 'thin-provisioning-tools' 'mdadm' 'xfsprogs')
  provides=('lvm2')
  conflicts=('lvm' 'mkinitcpio<0.7' 'lvm2')
  replaces=('lvm2')
  backup=('etc/lvm/lvm.conf')
  options=('!makeflags')
  install=lvm2.install

  cd LVM2-initramfs
  make DESTDIR="${pkgdir}" install_lvm2
  # install applib
  make -C liblvm DESTDIR="${pkgdir}" install
  # /etc directories
  install -d "${pkgdir}"/etc/lvm/{archive,backup}
  # mkinitcpio hook
  install -D -m644 "${srcdir}/lvm2_hook" "${pkgdir}/usr/lib/initcpio/hooks/lvm2"
  install -D -m644 "${srcdir}/lvm2_install" "${pkgdir}/usr/lib/initcpio/install/lvm2"
  
  # extra udev rule for lvmetad in non-systemd initramfs
  install -D -m644 "${srcdir}/LVM2-initramfs/udev/69-dm-lvm-metad.rules" "${pkgdir}/usr/lib/initcpio/udev/69-dm-lvm-metad.rules"
}
