# Maintainer: Eric Vidal <eric@obarun.org>
# based on the	original https://aur.archlinux.org/packages/dbus-nosystemd
# 						Maintainer: Alexey D. <lq07829icatm@rambler.ru>
# 						Contributor: Jan de Groot <jgc@archlinux.org>
# 						Contributor: Link Dupont <link@subpop.net>
#
pkgname=dbus-nosystemd
pkgver=1.8.20
pkgrel=1
pkgdesc="Freedesktop.org message bus system"
url="http://www.freedesktop.org/Software/dbus"
arch=(x86_64)
license=('GPL' 'custom')
groups=('eudev-base')
# dep on shadow for install scriptlet FS#29341
depends=('expat' 'coreutils' 'filesystem' 'shadow' "libdbus>=${pkgver}")
makedepends=('libx11' 'xmlto' 'docbook-xsl')
optdepends=('libx11: dbus-launch support'
            'dbus-openrc: dbus openrc initscript')
provides=('dbus-core' "dbus=${pkgver}")
conflicts=('dbus-core' 'dbus' 'dbus-eudev')
replaces=('dbus-core' 'dbus' 'dbus-eudev')
install=dbus-nosystemd.install
source=(http://dbus.freedesktop.org/releases/dbus/dbus-$pkgver.tar.gz
		30-dbus
		dbus.run.runit
		dbus.finish.runit
		dbus.log.run.runit
		dbus.config.log.runit)
md5sums=('b49890bbabedab3a1c3f4f73c7ff8b2b'
         '6683a05bd749929ef9442816c22c3268'
         'ef7d6dd5c2b073aac373a883d4a6995c'
         'b31b01a265e4b50b72da2677048fd1c1'
         'c63c966dbf18f05a15eb6d3446be0534'
         '84d6b4c2bffa87d2e16e08f2dec63834')

build() {
  cd dbus-$pkgver
  ./configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/dbus-1.0 \
      --with-dbus-user=dbus \
      --with-system-pid-file=/run/dbus/pid \
      --with-system-socket=/run/dbus/system_bus_socket \
      --with-console-auth-dir=/run/console/ \
      --enable-inotify \
      --disable-verbose-mode \
      --disable-static \
      --disable-tests \
      --disable-asserts \
      --disable-systemd

  make
}

package_dbus-nosystemd(){
  cd dbus-$pkgver

  # Disable installation of libdbus
  sed -i -e 's/^SUBDIRS = dbus/SUBDIRS =/' Makefile

  make DESTDIR="$pkgdir" install

  rm -rf "${pkgdir}/var/run"
  rm -rf "${pkgdir}/usr/lib/pkgconfig"

  install -Dm755 ../dbus.run.runit "$pkgdir/etc/sv/dbus/run"
  install -Dm755 ../dbus.finish.runit "$pkgdir/etc/sv/dbus/finish"
  install -Dm755 ../dbus.log.run.runit "$pkgdir/etc/sv/dbus/log/run"
  install -Dm644 ../dbus.config.log.runit "$pkgdir/var/log/dbus/config"
  install -Dm755 ../30-dbus "$pkgdir/etc/X11/xinit/xinitrc.d/30-dbus.sh"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/dbus/COPYING"
}


