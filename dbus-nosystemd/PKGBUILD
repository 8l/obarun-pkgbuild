# Maintainer: Eric Vidal <eric@obarun.org>
# based on the	original https://aur.archlinux.org/packages/dbus-nosystemd
# 						Maintainer: Alexey D. <lq07829icatm@rambler.ru>
# 						Contributor: Jan de Groot <jgc@archlinux.org>
# 						Contributor: Link Dupont <link@subpop.net>
#
pkgname=('dbus-nosystemd' 'libdbus-nosystemd')
pkgver=1.8.20
pkgrel=1
pkgdesc="Freedesktop.org message bus system"
url="http://www.freedesktop.org/Software/dbus"
arch=(x86_64)
license=('GPL' 'custom')
makedepends=('libx11' 'xmlto' 'docbook-xsl')
conflicts=('dbus-eudev')
install=dbus-nosystemd.install
source=(http://dbus.freedesktop.org/releases/dbus/dbus-$pkgver.tar.gz
		30-dbus)
md5sums=('b49890bbabedab3a1c3f4f73c7ff8b2b'
         '6683a05bd749929ef9442816c22c3268')

build() {
  cd dbus-$pkgver
  ./configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/dbus-1.0 \
      --with-dbus-user=dbus \
      --with-system-pid-file=/run/dbus/pid \
      --with-system-socket=/run/dbus/system_bus_socket \
      --with-console-auth-dir=/run/console/ \
      --enable-inotify \
      --disable-verbose-mode \
      --disable-static \
      --disable-tests \
      --disable-asserts \
      --disable-systemd

  make
}

package_dbus-nosystemd(){
	
	depends=('libdbus-nosystemd' 'expat' 'bash')
	optdepends=('dbus-runitserv: dbus service for runit'
				'libx11: dbus-launch support')
	provides=('dbus-core' 'dbus')
	conflicts=('dbus-core' 'dbus')
	replaces=('dbus-core' 'dbus')
  
  cd dbus-$pkgver

  # Disable installation of libdbus
  sed -i -e 's/^SUBDIRS = dbus/SUBDIRS =/' Makefile

  make DESTDIR="$pkgdir" install

  rm -rf "${pkgdir}/var/run"
  rm -rf "${pkgdir}/usr/lib/pkgconfig"

  install -Dm755 ../30-dbus "$pkgdir/etc/X11/xinit/xinitrc.d/30-dbus.sh"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/dbus/COPYING"
}
package_libdbus-nosystemd(){
  pkgdesc="DBus library"
  depends=('glibc')
  provides=('libdbus')
  conflicts=('libdbus')
  replaces=('libdbus')
  
  cd dbus-$pkgver
  make DESTDIR="$pkgdir" -C dbus install
  make DESTDIR="$pkgdir" install-data-am
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/libdbus/COPYING"
}

