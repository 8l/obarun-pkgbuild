# Maintainer: Eric Vidal <eric@obarun.org>
# based on the	original https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/xf86-input-libinput
# 						 Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgname=xf86-input-libinput-nosystemd
pkgver=0.13.0
pkgrel=1
pkgdesc="Generic input driver for the X.Org server based on libinput"
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libinput-nosystemd')
makedepends=('xorg-server-devel-nosystemd' 'X-ABI-XINPUT_VERSION=21.1' 'libxi' 'libx11' 'resourceproto' 'scrnsaverproto')
conflicts=('xorg-server<1.16' 'X-ABI-XINPUT_VERSION<21.1' 'X-ABI-XINPUT_VERSION>=22' 'xf86-input-libinput')
groups=('xorg-drivers' 'xorg')
source=(http://xorg.freedesktop.org/releases/individual/driver/${pkgname%-nosystemd}-${pkgver}.tar.bz2
        90-libinput.conf
        0001-Fedora-use-unaccelerated-vmask-on-input-ABI-21.patch)
sha256sums=('b9535c58904f47e8f85fee045b4a2b549c91216ddc9b0ee733d47d1b049fe3da'
            '727f6c92c93e7a48781a6fddf06f0a120d284020e3d96ba44d0d0d394cac5d8d'
            '3384152c46b39cdc6f091b4816f8fe07e450f399908b024f8e64de6f12b7a9d5')


prepare() {
  cd ${pkgname%-nosystemd}-${pkgver}

  # FS#45229 - not merged upstream
  patch -Np1 -i../0001-Fedora-use-unaccelerated-vmask-on-input-ABI-21.patch
}

build() {
  cd ${pkgname%-nosystemd}-${pkgver}

  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd ${pkgname%-nosystemd}-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/X11/xorg.conf.d"
  install -m644 ../90-libinput.conf "${pkgdir}/usr/share/X11/xorg.conf.d/"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname%-nosystemd}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname%-nosystemd}/"
}

