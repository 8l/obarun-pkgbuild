# Maintainer: Eric Vidal <eric@obarun.org>
# based on the	original https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/xf86-input-libinput
# 						 Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgname=xf86-input-libinput-nosystemd
pkgver=0.14.0
pkgrel=2
pkgdesc="Generic input driver for the X.Org server based on libinput"
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libinput-nosystemd')
makedepends=('xorg-server-devel-nosystemd' 'X-ABI-XINPUT_VERSION=21.1' 'libxi' 'libx11' 'resourceproto' 'scrnsaverproto')
provides=('xf86-input-libinput')
replaces=('xf86-input-libinput')
conflicts=('xorg-server<1.16' 'X-ABI-XINPUT_VERSION<21.1' 'X-ABI-XINPUT_VERSION>=22' 'xf86-input-libinput')
groups=('xorg-drivers' 'xorg')
source=(http://xorg.freedesktop.org/releases/individual/driver/${pkgname%-nosystemd}-${pkgver}.tar.bz2
        0001-Fedora-use-unaccelerated-vmask-on-input-ABI-21.patch)
sha256sums=('e8d05798a556e5ea0d9f1823bb632e425487e8bcf89711ed93b079bbee742265'
            '9f3c75da4918ab591c7c0c7267a3d3fb4154e30f4c7175b4cf53f07fed34d637')


prepare() {
  cd ${pkgname%-nosystemd}-${pkgver}

  # FS#45229 - not merged upstream
  patch -Np1 -i../0001-Fedora-use-unaccelerated-vmask-on-input-ABI-21.patch
}

build() {
  cd ${pkgname%-nosystemd}-${pkgver}

  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd ${pkgname%-nosystemd}-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/X11/xorg.conf.d"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname%-nosystemd}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname%-nosystemd}/"
}

