# Maintainer: Eric Vidal <eric@obarun.org>
# based on the	original https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/syslog-ng
# 							Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=syslog-ng-nosystemd
pkgver=3.6.3
pkgrel=1
pkgdesc="Next-generation syslogd with advanced networking and filtering capabilities"
arch=('i686' 'x86_64')
url="http://www.balabit.com/network-security/syslog-ng/"
license=('GPL2' 'LGPL2.1')
depends=('awk' 'eventlog' 'glib2' 'libdbi')
provides=('syslog-ng')
replaces=('syslog-ng')
conflicts=('syslog-ng')
makedepends=('python2' 'libxslt' 'json-c')
optdepends=('logrotate: for rotating log files'
	    'json-c: for json-plugin')
backup=('etc/syslog-ng/scl.conf'
        'etc/syslog-ng/syslog-ng.conf'
        'etc/logrotate.d/syslog-ng')
source=(http://www.balabit.com/downloads/files/syslog-ng/sources/$pkgver/source/${pkgname%-nosystemd}_$pkgver.tar.gz
        syslog-ng.conf syslog-ng.logrotate)
sha1sums=('33a8983d8324abf88975838149cbb772f5860d8a'
          '3e7ec4f3f68265aaa98f37338f801c5c22b85c17'
          '949128fe3d7f77a7aab99048061f885bc758000c')


build() {
  cd ${pkgname%-nosystemd}-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc/syslog-ng --libexecdir=/usr/lib \
    --sbindir=/usr/bin --localstatedir=/var/lib/syslog-ng --datadir=/usr/share/syslog-ng \
    --with-pidfile-dir=/run --disable-spoof-source --enable-ipv6 --enable-sql \
    --enable-systemd=no --with-systemdsystemunitdir=no \
    --enable-manpages --with-jsonc=system
  make
}

check() {
  cd ${pkgname%-nosystemd}-$pkgver
  make check
}

package() {
  make -C ${pkgname%-nosystemd}-$pkgver DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir/var/lib/syslog-ng" "$pkgdir/etc/syslog-ng/patterndb.d"
  install -Dm644 "$srcdir/syslog-ng.conf" "$pkgdir/etc/syslog-ng/syslog-ng.conf"
  install -Dm644 "$srcdir/syslog-ng.logrotate" "$pkgdir/etc/logrotate.d/syslog-ng"
}
