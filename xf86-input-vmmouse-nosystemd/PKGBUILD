# Maintainer: Eric Vidal <eric@obarun.org>
# based on the	original https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/xf86-input-vmmouse
# 						Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-input-vmmouse-nosystemd
pkgver=13.1.0
pkgrel=2
pkgdesc="X.org VMWare Mouse input driver"
arch=(x86_64)
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('glibc' 'sh')
makedepends=('xorg-server-devel-nosystemd' 'X-ABI-XINPUT_VERSION=21.1' 'resourceproto' 'scrnsaverproto')
provides=('xf86-input-vmmouse')
replaces=('xf86-input-vmmouse')
conflicts=('xorg-server<1.16' 'X-ABI-XINPUT_VERSION<21.1' 'X-ABI-XINPUT_VERSION>=22' 'xf86-input-vmmouse')
groups=('xorg-drivers' 'xorg')
source=(${url}/releases/individual/driver/${pkgname%-nosystemd}-${pkgver}.tar.bz2)
sha256sums=('0af558957ac1be1b2863712c2475de8f4d7f14921fd01ded2e2fde4921b19319')

build() {
  cd ${pkgname%-nosystemd}-${pkgver}
  ./configure --prefix=/usr \
    --with-udev-rules-dir=/usr/lib/udev/rules.d
  make
}

package() {
  cd ${pkgname%-nosystemd}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
  rm -rfv ${pkgdir}/usr/{lib,share}/hal
}
