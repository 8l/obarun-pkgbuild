# Maintainer: Eric Vidal <eric@obarun.org>
# based on the	original https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/upower
# 						Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=upower-nosystemd
pkgver=0.99.3
pkgrel=1
pkgdesc="Abstraction for enumerating power devices, listening to device events and querying history and statistics"
arch=('x86_64')
url="http://upower.freedesktop.org"
license=('GPL')
depends=('libusb' 'dbus-glib' 'libimobiledevice' 'libgudev')
provides=('upower')
replaces=('upower')
conflicts=('upower')
makedepends=('intltool' 'docbook-xsl' 'gobject-introspection' 'python2')
backup=('etc/UPower/UPower.conf')
source=(http://upower.freedesktop.org/releases/${pkgname%-nosystemd}-$pkgver.tar.xz)
sha256sums=('697199bcc113bb069e6fb6bf4135536a702ba9847e46c5fc6df87d19624ddd38')

build() {
  cd ${pkgname%-nosystemd}-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname \
    --disable-static \
    --with-udevrulesdir=/usr/lib/udev/rules.d \
    --with-systemdsystemunitdir=no \
    --with-systemdutildir=no
  make
}

package() {
  cd ${pkgname%-nosystemd}-$pkgver
  make DESTDIR="$pkgdir" install
}
