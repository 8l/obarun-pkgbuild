# Maintainer: Eric Vidal <eric@obarun.org>
# based on the	original https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/dhcpcd
# 						Maintainer: Ronald van Haren <ronald.archlinux.org>
# 						Contributor: Tom Killian <tom.archlinux.org>
# 						Contributor: Judd Vinet <jvinet.zeroflux.org>

pkgname=dhcpcd-nosystemd
pkgver=6.9.2
pkgrel=4
pkgdesc="RFC2131 compliant DHCP client daemon"
url="http://roy.marples.name/projects/dhcpcd/"
arch=('x86_64')
license=('BSD')
groups=('base')
depends=('glibc' 'sh' 'udev')
provides=('dhcp-client' 'dhcpcd')
replaces=('dhcpcd')
conflicts=('dhcpcd')
backup=('etc/dhcpcd.conf')
options=('emptydirs')  # We Need the Empty /var/lib/dhcpcd Directory
source=("http://roy.marples.name/downloads/${pkgname%-nosystemd}/${pkgname%-nosystemd}-$pkgver.tar.xz"
        dhcpcd.conf)
sha1sums=('a79ea29f02b556e929dda497f6ededfc3c7786b2'
          '341e1ba3b10367dad181403f112e110a47a19aa6')

build() {
  cd "${srcdir}/${pkgname%-nosystemd}-${pkgver}"

  # configure variables
  ./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localestatdir=/var \
		--sbindir=/usr/bin \
		--libexecdir=/usr/lib/dhcpcd \
		--dbdir=/var/lib/dhcpcd \
		--rundir=/run \
		
  # Build
  make
}

check() {
  cd "${srcdir}/${pkgname%-nosystemd}-${pkgver}"

  make test
}

package() {
  cd "${srcdir}/${pkgname%-nosystemd}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # Install License
  install -d "$pkgdir/usr/share/licenses/${pkgname%-nosystemd}"
  sed 26q "$srcdir/${pkgname%-nosystemd}-$pkgver/control.h" \
      >>"$pkgdir/usr/share/licenses/${pkgname%-nosystemd}/LICENSE"

  # Set Options in /etc/dhcpcd.conf
  install -m644 "${srcdir}/dhcpcd.conf" "${pkgdir}/etc/dhcpcd.conf"

}
