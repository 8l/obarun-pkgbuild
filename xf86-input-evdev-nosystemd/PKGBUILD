# Maintainer: Eric Vidal <eric@obarun.org>
# based on the	original https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/xf86-input-evdev
#						Maintainer: Jan de Groot <jgc@archlinux.org>
# 						Contributor: Alexander Baldeck <Alexander@archlinux.org

pkgname=xf86-input-evdev-nosystemd
pkgver=2.9.2
pkgrel=2
pkgdesc="X.org evdev input driver"
arch=(x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('glibc' 'mtdev' 'libevdev')
makedepends=('xorg-server-devel-nosystemd' 'resourceproto' 'scrnsaverproto')
provides=('xf86-input-evdev')
replaces=('xf86-input-evdev')
conflicts=('xorg-server<1.16.0' 'X-ABI-XINPUT_VERSION<21' 'X-ABI-XINPUT_VERSION>=22' 'xf86-input-evdev')
options=('!makeflags')
groups=('xorg-drivers' 'xorg')
source=(${url}/releases/individual/driver/${pkgname%-nosystemd}-${pkgver}.tar.bz2)
sha256sums=('792329b531afc6928ccda94e4b51a5520d4ddf8ef9a00890a5d0d31898acefec')

build() {
  cd ${pkgname%-nosystemd}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname%-nosystemd}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname%-nosystemd}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname%-nosystemd}/"
}
