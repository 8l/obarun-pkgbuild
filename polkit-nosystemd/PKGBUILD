# Maintainer: Eric Vidal <eric@obarun.org>
# based on the	original https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/polkit
# 						Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=polkit-nosystemd
pkgver=0.113
pkgrel=4
pkgdesc="Application development toolkit for controlling system-wide privileges"
arch=(x86_64)
license=(LGPL)
url="http://www.freedesktop.org/wiki/Software/polkit"
depends=(glib2 pam expat js17)
makedepends=(intltool gtk-doc gobject-introspection git)
provides=('polkit' 'polkit-consolekit')
replaces=('polkit' 'polkit-consolekit')
conflicts=('polkit' 'polkit-consolekit')
install=polkit.install
source=(http://www.freedesktop.org/software/polkit/releases/${pkgname%-nosystemd}-$pkgver.tar.gz
		polkit.pam)
md5sums=('4b77776c9e4f897dcfe03b2c34198edf'
         '6564f95878297b954f0572bc1610dd15')

build() {
  cd ${pkgname%-nosystemd}-$pkgver

  ./configure 	--prefix=/usr \
				--sysconfdir=/etc \
				--localstatedir=/var \
				--libexecdir=/usr/lib/polkit-1 \
				--enable-libsystemd-login=no \
				--disable-static \
				--enable-gtk-doc \
				--with-os-type=redhat
  make
}

check(){
	cd ${pkgname%-nosystemd}-$pkgver
	make -k check || :
}

package() {
  cd ${pkgname%-nosystemd}-$pkgver
  make DESTDIR="$pkgdir" install

  chown 102 "$pkgdir/etc/polkit-1/rules.d"
  chown 102 "$pkgdir/usr/share/polkit-1/rules.d"
  
  install -m644 "$srcdir/polkit.pam" "$pkgdir/etc/pam.d/polkit-1"

}
