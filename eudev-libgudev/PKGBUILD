# Maintainer: Eric Vidal <eric@obarun.org>
# based on the	original https://aur.archlinux.org/cgit/aur.git/log/PKGBUILD?h=eudev-git
# 			Maintainer: Alexey D. <lq07829icatm@rambler.ru>
# 			Contributor: Ivailo Monev <xakepa10@gmail.com>

pkgname=eudev-libgudev
pkgver=3.1.5
pkgrel=1
pkgdesc="The userspace dev tools (udev) forked by Gentoo"
arch=('x86_64')
url="https://github.com/gentoo/eudev"
license=('GPL')
depends=('glib2' 'kbd' 'kmod' 'hwids' 'util-linux' 'bash')
makedepends=('git' 'gobject-introspection' 'gperf' 'libxslt' 'docbook-xsl')
optdepends=('upower-pm-utils: pm-utils support')
conflicts=('eudev' 'eudev-git' 'udev' 'systemd' 'libsystemd' 'systemd-tools' 'libgudev')
replaces=('eudev' 'udev' 'libsystemd' 'systemd-tools' 'udev' 'systemd')
provides=('libgudev' 'eudev')
backup=('etc/udev/udev.conf' 'usr/lib/udev/rules.d/80-net-name-slot.rules')
options=(!makeflags !libtool)
source=('git://github.com/gentoo/eudev.git'
		'initcpio_hooks'
		'initcpio_install')
sha256sums=('SKIP'
            '892ce43218e0a458981bbce451252c8987dc398e60b8de288e7542b8f2409c13'
            '77dd1fd318b4456409aceb077f060b87944defb07cf39d29ad1968dc6f361875')
_gitname="eudev"

#pkgver()
#{
#	cd "${srcdir}/${_gitname}"

#	Date of last commit
#	git log -1 --format="%ci" HEAD | cut -d\  -f1 | tr -d '-'
#}

build() {
	cd "${srcdir}/${_gitname}"

  	if [ -f "Makefile" ];then
   	 msg2 "Cleaning up..."
   	 make clean
 	fi

	./autogen.sh
	./configure \
		--prefix=/usr \
		--with-rootprefix=/usr \
		--sysconfdir=/etc \
		--libdir=/usr/lib \
		--sbindir=/usr/bin \
		--with-html-dir=/usr/share/doc/${pkgname%-libgudev}-${pkgver}/html \
		--enable-gudev \
		--enable-introspection \
		--enable-kmod \
		--enable-split-usr
	make
}

package() {
	cd "${srcdir}/${_gitname}"
	make DESTDIR="${pkgdir}" install

	for i in "${pkgdir}/usr/lib/udev/rules.d/"*.rules; do
		sed -i -e 's#GROUP="dialout"#GROUP="uucp"#g;
                            s#GROUP="tape"#GROUP="storage"#g;
                            s#GROUP="cdrom"#GROUP="optical"#g' "${i}"
	done

	install -Dm644 "${srcdir}/initcpio_hooks" "${pkgdir}/usr/lib/initcpio/hooks/udev"
	install -Dm644 "${srcdir}/initcpio_install" "${pkgdir}/usr/lib/initcpio/install/udev"
	
	# input group is not used in Arch Linux at this moment
	sed '/GROUP="input"/d' -i "${pkgdir}/usr/lib/udev/rules.d/50-udev-default.rules"

  	# Getting udev version
  	udevver=$(grep UDEV_VERSION configure.ac | egrep -o "[0-9]{3}")
  	provides+=("udev=$udevver")
  	provides+=("systemd=$udevver")
 	provides+=("libsystemd=$udevver")
  	provides+=("systemd-tools=$udevver")

}
