# Maintainer: Eric Vidal <eric@obarun.org>
# based on the	original https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/cups-pdf
# 						Maintainer: Andreas Radke <andyrtr at archlinux.org>
# 						Contributor: Thomas Baechler <thomas.baechler@rwth-aachen.de>

pkgname=cups-pdf-nosystemd
pkgver=2.6.1
pkgrel=2
pkgdesc="PDF printer for cups"
arch=(x86_64)
depends=('cups-nosystemd' 'ghostscript-libcups-nosystemd')
conflicts=(cups-pdf)
install=cups-pdf.install
url="http://cip.physik.uni-wuerzburg.de/~vrbehr/cups-pdf"
license=('GPL2')
source=(http://cip.physik.uni-wuerzburg.de/~vrbehr/cups-pdf/src/cups-pdf_$pkgver.tar.gz)
backup=(etc/cups/cups-pdf.conf)
md5sums=('65f3fd525c4a9b1d736b91594b3166d5')

build() {
  cd $srcdir/${pkgname%-nosystemd}-$pkgver/src
  [ -z "$CC" ] && CC=gcc
  $CC $CFLAGS -Wall -o cups-pdf cups-pdf.c
}

package() {
  cd $srcdir/${pkgname%-nosystemd}-$pkgver/src
  install -D -m700 cups-pdf $pkgdir/usr/lib/cups/backend/cups-pdf

  # Install Postscript Color printer
  cd ../extra
  install -D -m644 CUPS-PDF.ppd $pkgdir/usr/share/cups/model/CUPS-PDF.ppd

  # Install config file
  install -D -m644 cups-pdf.conf $pkgdir/etc/cups/cups-pdf.conf
  
  # use lp group from cups pkg FS#36769
  chgrp -R lp ${pkgdir}/etc/cups
}
